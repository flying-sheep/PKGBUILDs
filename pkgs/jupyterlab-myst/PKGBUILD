# Maintainer: Phil A. <flying-sheep@web.de>
# Contributor: Anthony Wang <ta180m@gmail.com>
pkgname=jupyterlab-myst
pkgver=2.6.0
pkgrel=1
pkgdesc='Use MyST in JupyterLab'
arch=(any)
url=https://github.com/executablebooks/$pkgname
license=(BSD)
depends=(jupyter-server)
makedepends=(
	# build
	python-build
	python-hatchling
	jupyterlab  # jlpm
	python-hatch-jupyter-builder
	python-hatch-nodejs-version
	nodejs
	# package
	python-installer
)
provides=(python-jupyterlab-myst)
source=("$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('b3c5fb7cd3904c64bfd490b3de8d6351c8aa7c3d52ac1eb5ff8c69e9530f71f0')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	python -m build --wheel --no-isolation --skip-dependency-check
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	python -m installer --destdir="$pkgdir" dist/*.whl
	mv "$pkgdir"/{usr/,}etc
}
